System transporttrolley

Request  pickup         	: pickup( ARG )
Reply    pickupDone     	: pickupDone( ARG )
Request  deposit        	: deposit( ARG )
Reply    depositDone    	: depositDone( ARG )
Request  moveToDestination  : info( X_DESTINATION, Y_DESTINATION )
Reply    destinationReached : destinationReached( ARG )

Request trolleyRotate : trolleyRotate(DIR)
Reply   rotateDone    : rotateDone(_)

Request  findPath           : findPath( X_DESTINATION, Y_DESTINATION )
Reply    path               : path( PATH )

Request  dopath             : dopath( PATH  )
Reply 	 dopathdone     : dopathdone( ARG ) 
Reply 	 dopathfail     : dopathfail( ARG )

//SONAR
Dispatch stop  	 	: stop(_)
Dispatch resume 	: resume(_)
Event 	 alarm 		: alarm(STOP)
Event    sonardata  : distance( V )   

 

Context ctx_basicrobot 				  ip [ host="robot" port=8020 ]
Context ctx_transporttrolley	      ip [ host="localhost" port=8051 ]


ExternalQActor pathexec context ctx_basicrobot

QActor transporttrolley context ctx_transporttrolley {
	
	State s0 initial {
		printCurrentMessage	
	}
	
	Transition t0 whenRequest moveToDestination -> move
				  whenRequest trolleyRotate     -> rotate
	
	State move {
		printCurrentMessage
		updateResource [# "work" #]
		
		onMsg( moveToDestination : info( X_DESTINATION, Y_DESTINATION )) {
			
			request transporttrolley_mover -m findPath : findPath( $payloadArg(0), $payloadArg(1) )
		}
	}
	
	Transition t1 	whenReply dopathdone -> destinationReached
	
	State destinationReached {
		printCurrentMessage
		replyTo moveToDestination with destinationReached : destinationReached(ok)
	}
	
	Transition t2 whenRequest pickup -> handle_pickup
				  whenRequest deposit -> handle_deposit
				  whenRequest moveToDestination -> move
				  whenRequest trolleyRotate -> rotate
	
	
	State handle_pickup {
		printCurrentMessage
		delay 5000
		replyTo pickup with pickupDone : pickupDone(true)
	}
	
	Transition t3 whenRequest moveToDestination -> move
	
	
	State handle_deposit {
		printCurrentMessage
		delay 5000
		replyTo deposit with depositDone : depositDone(true)
	}
	
	Transition t4 	whenRequest moveToDestination -> move
	
	State rotate {
		printCurrentMessage
		onMsg ( trolleyRotate : trolleyRotate(DIR)){
			
			[#
				val Dir = payloadArg(0)	
			#]
			request transporttrolley_mover -m trolleyRotate : trolleyRotate($Dir)
		}
	}
	
	Transition t5 whenReply rotateDone -> rotateDone
	
	State rotateDone {
		replyTo trolleyRotate with rotateDone : rotateDone(OK)
	}
	Goto s0

}



QActor transporttrolley_mover context ctx_transporttrolley { 
	[#
		var RemainingPath = ""
	#] 
	
	State s0 initial {
		
		printCurrentMessage
		[# 
			unibo.kotlin.planner22Util.initAI();
			unibo.kotlin.planner22Util.loadRoomMap("mapRoomEmpty");
			unibo.kotlin.planner22Util.showMap();
			unibo.kotlin.planner22Util.showCurrentRobotState();
		#]	
	}
	
	Goto idle
	
	State idle {
		printCurrentMessage
		updateResource [# "trolleyState(idle)" #]
		
	}
	
	Transition t0 	whenRequest findPath -> findThePath
				    whenRequest trolleyRotate -> rotate
	
	State findThePath {
		updateResource [# "trolleyState(work)" #]
		[#
			var MovesToDo = ""	
		#]
		printCurrentMessage

		onMsg( findPath : findPath( X_DESTINATION, Y_DESTINATION )) {
			[#
				val X_Destination = payloadArg(0)
				val Y_Destination = payloadArg(1)
				unibo.kotlin.planner22Util.planForGoal( X_Destination, Y_Destination )
				MovesToDo = unibo.kotlin.planner22Util.getActions().joinToString("")
				pathut.setPath(MovesToDo)
				pathut.updateMap()
			#] 
		}

		println(MovesToDo)
		request pathexec -m dopath : dopath($MovesToDo)
			
	}
	
	Transition t1 	whenReply dopathdone -> pathDone
					whenReply dopathfail -> handle_resume
					
	
	State pathDone {
		printCurrentMessage
		
		replyTo findPath with dopathdone : dopathdone(ok)
	}
	

	Goto idle
	
	State handle_resume {
		printCurrentMessage

		
		onMsg (dopathfail   : dopathfail( ARG )) {
			[#
				var RemainingPath = payloadArg(0)
			#]
			if [# RemainingPath.length == 1 || RemainingPath.length == 0#]{
				[# RemainingPath = "" #]
				request pathexec -m dopath : dopath($RemainingPath) 
			}
			else{
				request pathexec -m dopath : dopath($RemainingPath) 
			}
		}

	}
	
	Transition t2 whenReply dopathdone -> pathDone
				  whenReply dopathfail -> handle_resume
	
	State rotate {
		printCurrentMessage
		onMsg ( trolleyRotate : trolleyRotate(DIR)){
			[#
				val Path = pathut.getRotation(payloadArg(0))
				pathut.setPath(Path)
				pathut.updateMap()
			#]
			request pathexec -m dopath : dopath($Path)
		}
	}
	
	Transition t5 whenReply dopathdone -> rotateDone
			      whenReply dopathfail -> handle_resume
	
	State rotateDone {
		replyTo trolleyRotate with rotateDone : rotateDone(OK)
	}
	Goto idle

}